plugins {
	id 'java' //java support
	id 'groovy' // groovy support
	id 'pmd' // code check, working on source code
	id 'com.github.spotbugs' version '4.4.4' // code check, working on byte code
	id 'com.diffplug.spotless' version '5.0.0'//code format
	id 'org.openjfx.javafxplugin' version '0.0.9' // java 11 javafx plugin for gradle
	id 'de.undercouch.download' version '4.1.0'
	id 'kr.motd.sphinx' version '2.9.0' // documentation generation
	id 'jacoco' // java code coverage plugin
	id "org.sonarqube" version "3.0" // sonarqube
}

ext {
	//version (changing these should be considered thoroughly!)
	javaVersion = JavaVersion.VERSION_11
	unitsOfMeasurementVersion = '1.0.10'

	scriptsLocation = 'gradle' + File.separator + 'scripts' + File.separator //location of script
}

group = 'com.github.ie3-institute'
description = 'OSMoGrid'
version = '1.0-SNAPSHOT'


apply from: scriptsLocation + 'tests.gradle'
apply from: scriptsLocation + 'pmd.gradle'
apply from: scriptsLocation + 'spotbugs.gradle'
apply from: scriptsLocation + 'spotless.gradle'
apply from: scriptsLocation + 'checkJavaVersion.gradle'
//apply from: scriptsLocation + 'selfContainedJar.gradle'
apply from: scriptsLocation + 'documentation.gradle'
apply from: scriptsLocation + 'jacoco.gradle' // jacoco java code coverage
//apply from: scriptsLocation + 'mavenCentralPublish.gradle'
apply from: scriptsLocation + 'sonarqube.gradle'
apply from: scriptsLocation + 'vcs.gradle'

repositories {
	jcenter() //searches in bintray's repository 'jCenter', which contains Maven Central
	maven { url 'https://www.jitpack.io' } // allows github repos as dependencies
	maven { url 'https://oss.sonatype.org/service/local/repositories/snapshots/content' } // sonatype snapshots repo
}

dependencies {
	//	 ieÂ³ power system utils
	compile ('com.github.ie3-institute:PowerSystemUtils:1.3.2-SNAPSHOT') {
		exclude group: 'org.slf4j', module: 'slf4j-api'
		exclude group: 'com.github.ie3-institute'
		exclude group: 'tec.uom.se'
		exclude group:'com.github.johanneshiry', module:'OSMonaut'
	}
	compile ('com.github.ie3-institute:PowerSystemUtils:1.1-SNAPSHOT') {
		exclude group: 'org.slf4j', module: 'slf4j-api'
		exclude group: 'com.github.ie3-institute'
		exclude group: 'tec.uom.se'
	}

	compile "tec.uom:uom-se:$unitsOfMeasurementVersion"

	// JTS Topology Suite for GeoPositions, License: EPL 1.0 / EDL 1.0
	compile 'org.locationtech.jts:jts-core:1.16.1'
	compile 'org.locationtech.jts.io:jts-io-common:1.16.1'

	// Graphs
	compile 'org.jgrapht:jgrapht-core:1.3.1'

	// testing
	testCompile 'org.junit.jupiter:junit-jupiter:5.5.2'
	testCompile 'org.spockframework:spock-core:2.0-M3-groovy-3.0'
	testCompile 'org.objenesis:objenesis:3.1' // Mock creation with constructor parameters

	// logging
	compile 'org.apache.logging.log4j:log4j-api:+' // log4j
	compile 'org.apache.logging.log4j:log4j-core:+' // log4j
	compile 'org.apache.logging.log4j:log4j-slf4j-impl:+' // log4j -> slf4j

	// pbf parser
	compile group: 'com.github.johanneshiry', name: 'OSMonaut', version: 'v1.1.1'

	// javafx
	compile group: 'org.openjfx', name: 'javafx-fxml', version: javaVersion.toString()
	compile group: 'org.openjfx', name: 'javafx-swing', version: javaVersion.toString()
	compile group: 'org.openjfx', name: 'javafx-graphics', version: javaVersion.toString()
	compile group: 'org.openjfx', name: 'javafx-web', version: javaVersion.toString()
}

wrapper {
	gradleVersion = '6.5.1'
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

task printVersion {
	doLast {
		println project.version
	}
}