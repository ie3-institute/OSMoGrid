plugins {
	id 'java' //java support
	id 'groovy' // groovy support
	id 'pmd' // code check, working on source code
	id 'com.diffplug.spotless' version '5.12.5'//code format
	id 'com.github.spotbugs' version '4.7.2' // code check, working on byte code
	id 'org.openjfx.javafxplugin' version '0.0.10' // java 11 javafx plugin for gradle
	id 'de.undercouch.download' version '4.1.2'
	id 'kr.motd.sphinx' version '2.10.1' // documentation generation
	id 'jacoco' // java code coverage plugin
	id "org.sonarqube" version "3.3" // sonarqube
}

ext {
	//version (changing these should be considered thoroughly!)
	javaVersion = JavaVersion.VERSION_11
	tscfgVersion = '0.9.986'
	scriptsLocation = 'gradle' + File.separator + 'scripts' + File.separator //location of script
}

group = 'com.github.ie3-institute'
description = 'OSMoGrid'
version = '1.0-SNAPSHOT'

apply from: scriptsLocation + 'tests.gradle'
apply from: scriptsLocation + 'pmd.gradle'
apply from: scriptsLocation + 'spotbugs.gradle'
apply from: scriptsLocation + 'spotless.gradle'
apply from: scriptsLocation + 'checkJavaVersion.gradle'
apply from: scriptsLocation + 'documentation.gradle'
apply from: scriptsLocation + 'jacoco.gradle' // jacoco java code coverage
apply from: scriptsLocation + 'sonarqube.gradle'
apply from: scriptsLocation + 'vcs.gradle'

repositories {
	mavenCentral() //searches in bintray's repository 'jCenter', which contains Maven Central
	maven { url 'https://www.jitpack.io' } // allows github repos as dependencies
	maven { url 'https://oss.sonatype.org/service/local/repositories/snapshots/content' } // sonatype snapshots repo
}

dependencies {
	//	 ie³ power system utils
	implementation('com.github.ie3-institute:PowerSystemUtils:1.5.3') {
		exclude group: 'org.slf4j', module: 'slf4j-api'
		exclude group: 'com.github.ie3-institute'
		exclude group: 'tec.uom.se'
		exclude group: 'com.github.johanneshiry', module: 'OSMonaut'
	}

	//	 ie³ power system data model
	implementation('com.github.ie3-institute:PowerSystemDataModel:2.0.0') {
		exclude group: 'org.slf4j', module: 'slf4j-api'
		exclude group: 'com.github.ie3-institute'
	}

	// JTS Topology Suite for GeoPositions, License: EPL 1.0 / EDL 1.0
	implementation 'org.locationtech.jts:jts-core:1.16.1'
	implementation 'org.locationtech.jts.io:jts-io-common:1.16.1'

	// Graphs
	implementation 'org.jgrapht:jgrapht-core:1.5.1'

	// logging
	implementation 'org.apache.logging.log4j:log4j-api:2.14.1' // log4j
	implementation 'org.apache.logging.log4j:log4j-core:2.14.1' // log4j
	implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.14.1' // log4j -> slf4j

	// pbf parser
	implementation group: 'com.github.johanneshiry', name: 'OSMonaut', version: 'v1.1.1'

	// javafx
	implementation group: 'org.openjfx', name: 'javafx-fxml', version: javaVersion.toString()
	implementation group: 'org.openjfx', name: 'javafx-swing', version: javaVersion.toString()
	implementation group: 'org.openjfx', name: 'javafx-graphics', version: javaVersion.toString()
	implementation group: 'org.openjfx', name: 'javafx-web', version: javaVersion.toString()

	implementation 'org.jxmapviewer:jxmapviewer2:2.6' // osm map viewer
	implementation 'org.glassfish.jaxb:jaxb-runtime:3.0.1'

	// required for xml parser (javax.xml.bind) (see: https://stackoverflow.com/questions/48204141/replacements-for-deprecated-jpms-modules-with-java-ee-apis/48279048#48279048)
	implementation 'javax.xml.bind:jaxb-api:2.3.1' // xml parsing
	implementation 'org.apache.commons:commons-lang3:3.12.0' // for HashCodeBuilder
	implementation group: 'math.geom2d', name: 'javaGeom', version: '0.11.1'
	implementation 'info.picocli:picocli:4.6.1' // command line interface

	spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:+'

	// testing
	testImplementation 'junit:junit:4.13.2'
	testImplementation 'cglib:cglib-nodep:3.3.0' // enables mocking of classes (in addition to interfaces)
	testImplementation 'org.mockito:mockito-core:3.11.2' // mocking framework
	testImplementation 'org.spockframework:spock-core:2.0-M3-groovy-3.0'
	testImplementation 'org.objenesis:objenesis:3.2' // Mock creation with constructor parameters

	// config //
	implementation 'com.typesafe:config:1.4.1'
	implementation "com.github.carueda:tscfg_2.13:$tscfgVersion"

}

wrapper {
	gradleVersion = '7.0'
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

task printVersion {
	doLast {
		println project.version
	}
}
