plugins {
  // todo revise
  id 'java' //java support
  id 'scala'
  id 'pmd' // code check, working on source code
  id 'com.diffplug.spotless' version '6.3.0'//code format
  id 'com.github.spotbugs' version '5.0.5' // code check, working on byte code
  id 'de.undercouch.download' version '5.0.1'
  id 'kr.motd.sphinx' version '2.10.1' // documentation generation
  id "com.github.maiflai.scalatest" version "0.32" // run scalatest without specific spec task
  id "org.sonarqube" version "3.3" // sonarqube
  id 'net.thauvin.erik.gradle.semver' version '1.0.4' // semantic versioning
}

ext {
  javaVersion = JavaVersion.VERSION_17

  scalaMajorVersion = '3'
  scalaVersion = '3.1.1'
  akkaVersion = '2.6.18'

  tscfgVersion = '0.9.995'

  scriptsLocation = 'gradle' + File.separator + 'scripts' + File.separator
  docsDirName = 'docs'
}

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(javaVersion.toString()))
  }
}

group = 'com.github.ie3-institute'
description = 'OSMoGrid'

apply from: scriptsLocation + 'pmd.gradle'
apply from: scriptsLocation + 'spotbugs.gradle'
apply from: scriptsLocation + 'spotless.gradle'
apply from: scriptsLocation + 'checkJavaVersion.gradle'
apply from: scriptsLocation + 'documentation.gradle'
apply from: scriptsLocation + 'sonarqube.gradle'
apply from: scriptsLocation + 'vcs.gradle'
apply from: scriptsLocation + 'semVer.gradle'
apply from: scriptsLocation + 'tscfg.gradle'

repositories {
  mavenCentral() //searches in bintray's repository 'jCenter', which contains Maven Central
  maven { url 'https://www.jitpack.io' } // allows github repos as dependencies// sonatype snapshot repo
  maven { url 'https://oss.sonatype.org/content/repositories/snapshots' } // Snapshot-Repository
}

dependencies {

  // scala core
  implementation "org.scala-lang:scala3-library_3:$scalaVersion"
  implementation 'org.scala-lang.modules:scala-parallel-collections_3:1.0.4'

  // akka
  implementation "com.typesafe.akka:akka-actor-typed_${scalaMajorVersion}:${akkaVersion}"
  implementation platform("com.typesafe.akka:akka-bom_$scalaMajorVersion:2.6.18")
  testImplementation "com.typesafe.akka:akka-actor-testkit-typed_$scalaMajorVersion:2.6.18"

  // logging
  implementation "com.typesafe.scala-logging:scala-logging_${scalaMajorVersion}:3.9.4"
  implementation 'ch.qos.logback:logback-classic:1.2.10'

  // testing
  testImplementation "org.scalatest:scalatest_$scalaMajorVersion:3.2.11"
  testRuntimeClasspath 'com.vladsch.flexmark:flexmark-all:0.64.0'
  testImplementation 'org.pegdown:pegdown:1.6.0'

  // config
  implementation 'com.typesafe:config:1.4.2'
  implementation "com.github.scopt:scopt_$scalaMajorVersion:4.0.1" // cmd args parser

  // todo CLEANUP following old dependencies
  //	 ie³ power system utils
  implementation('com.github.ie3-institute:PowerSystemUtils:2.0-SNAPSHOT') {
    exclude group: 'org.slf4j'
    exclude group: 'org.apache.logging.log4j'
    exclude group: 'com.github.ie3-institute'
    exclude group: 'com.github.johanneshiry', module: 'OSMonaut'
  }

  //	 ie³ power system data model
  implementation('com.github.ie3-institute:PowerSystemDataModel:2.1.0') {
    exclude group: 'org.slf4j'
    exclude group: 'org.apache.logging.log4j'
    exclude group: 'com.github.ie3-institute'
  }

  // Graphs
  implementation 'org.jgrapht:jgrapht-core:1.5.1'

  // pbf parser
  implementation 'com.github.johanneshiry:OSMonaut:v1.1.1'

  implementation 'org.jxmapviewer:jxmapviewer2:2.6' // osm map viewer
  implementation 'org.glassfish.jaxb:jaxb-runtime:3.0.2'

  // required for xml parser (javax.xml.bind) (see: https://stackoverflow.com/questions/48204141/replacements-for-deprecated-jpms-modules-with-java-ee-apis/48279048#48279048)
  implementation 'javax.xml.bind:jaxb-api:2.4.0-b180830.0359' // xml parsing
  implementation 'org.apache.commons:commons-lang3:3.12.0' // for HashCodeBuilder
  implementation 'math.geom2d:javaGeom:0.11.1'
  implementation 'info.picocli:picocli:4.6.3' // command line interface

  spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.11.0'

  // testing
  testImplementation 'junit:junit:4.13.2'
  testImplementation 'cglib:cglib-nodep:3.3.0' // enables mocking of classes (in addition to interfaces)
  testImplementation 'org.mockito:mockito-core:4.3.1' // mocking framework
  testImplementation 'org.scalatestplus:mockito-3-12_3:3.2.10.0' // syntactic sugar

  testImplementation 'org.spockframework:spock-core:2.1-groovy-3.0'
  testImplementation 'org.objenesis:objenesis:3.2' // Mock creation with constructor parameters

  // TODO: Remove. Only there for backwards compatibility in Java code
  implementation 'tech.units:indriya:2.1.2'
  implementation 'org.locationtech.jts:jts-core:1.18.2'
}

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

tasks.withType(Javadoc){
  options.encoding = 'UTF-8'
}

task printVersion {
  doLast {
    println project.version
  }
}

