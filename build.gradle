plugins {
  // todo revise
  id 'java' //java support
  id 'scala'
  id 'pmd' // code check, working on source code
  id 'com.diffplug.spotless' version '6.10.0'//code format
  id 'com.github.spotbugs' version '5.0.12' // code check, working on byte code
  id 'de.undercouch.download' version '5.2.0'
  id 'kr.motd.sphinx' version '2.10.1' // documentation generation
  id "com.github.maiflai.scalatest" version "0.32" // run scalatest without specific spec task
  id "org.sonarqube" version "3.4.0.2513" // sonarqube
  id "org.scoverage" version "7.0.1" // scala code coverage scoverage
  id 'net.thauvin.erik.gradle.semver' version '1.0.4' // semantic versioning
}

ext {
  javaVersion = JavaVersion.VERSION_17

  scalaVersion = '2.13'
  scalaBinaryVersion = '2.13.8'
  akkaVersion = '2.6.20'

  tscfgVersion = '0.9.995'
  scapegoatVersion = '1.4.15'

  scriptsLocation = 'gradle' + File.separator + 'scripts' + File.separator
  docsDirName = 'docs'
}

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(javaVersion.toString()))
  }
}

group = 'com.github.ie3-institute'
description = 'OSMoGrid'

apply from: scriptsLocation + 'pmd.gradle'
apply from: scriptsLocation + 'spotbugs.gradle'
apply from: scriptsLocation + 'spotless.gradle'
apply from: scriptsLocation + 'checkJavaVersion.gradle'
apply from: scriptsLocation + 'documentation.gradle'
apply from: scriptsLocation + 'sonarqube.gradle' // sonarqube config
apply from: scriptsLocation + 'scoverage.gradle' // scoverage scala code coverage
apply from: scriptsLocation + 'vcs.gradle'
apply from: scriptsLocation + 'semVer.gradle'
apply from: scriptsLocation + 'tscfg.gradle'

configurations {
  scalaCompilerPlugin
}

repositories {
  mavenCentral() //searches in bintray's repository 'jCenter', which contains Maven Central
  maven { url 'https://www.jitpack.io' } // allows github repos as dependencies// sonatype snapshot repo
  maven { url 'https://oss.sonatype.org/content/repositories/snapshots' } // Snapshot-Repository
}

dependencies {
  /* Scala compiler plugin for static code analysis */
  implementation "com.sksamuel.scapegoat:scalac-scapegoat-plugin_${scalaBinaryVersion}:${scapegoatVersion}"
  scalaCompilerPlugin "com.sksamuel.scapegoat:scalac-scapegoat-plugin_${scalaBinaryVersion}:${scapegoatVersion}"

  // scala core
  implementation "org.scala-lang:scala-library:${scalaBinaryVersion}"
  implementation group: 'org.scala-lang.modules', name: "scala-parallel-collections_${scalaVersion}", version: '1.0.4'

  // akka
  implementation "com.typesafe.akka:akka-actor-typed_${scalaVersion}:${akkaVersion}"
  testImplementation "com.typesafe.akka:akka-actor-testkit-typed_$scalaVersion:${akkaVersion}"

  // logging
  implementation "com.typesafe.scala-logging:scala-logging_${scalaVersion}:3.9.5"
  implementation 'ch.qos.logback:logback-classic:1.2.11'

  // testing
  testImplementation "org.scalatest:scalatest_$scalaVersion:3.2.13"
  testRuntimeOnly 'com.vladsch.flexmark:flexmark-all:0.64.0' //scalatest html output
  testImplementation 'org.pegdown:pegdown:1.6.0'

  // config
  implementation 'com.typesafe:config:1.4.2'
  implementation "com.github.scopt:scopt_$scalaVersion:4.1.0" // cmd args parser

  // osm4scala
  implementation 'com.acervera.osm4scala:osm4scala-core_2.13:1.0.10'

  // todo CLEANUP following old dependencies
  //	 ie³ power system utils
  implementation('com.github.ie3-institute:PowerSystemUtils:2.0-SNAPSHOT') {
    exclude group: 'org.slf4j'
    exclude group: 'org.apache.logging.log4j'
    exclude group: 'com.github.ie3-institute'
    exclude group: 'com.github.johanneshiry', module: 'OSMonaut'
  }

  //	 ie³ power system data model
  implementation('com.github.ie3-institute:PowerSystemDataModel:2.1.0') {
    exclude group: 'org.slf4j'
    exclude group: 'org.apache.logging.log4j'
    exclude group: 'com.github.ie3-institute'
  }

  // Graphs
  implementation 'org.jgrapht:jgrapht-core:1.5.1'

  // pbf parser
  implementation 'com.github.johanneshiry:OSMonaut:v1.1.1'

  implementation 'org.jxmapviewer:jxmapviewer2:2.6' // osm map viewer
  implementation 'org.glassfish.jaxb:jaxb-runtime:4.0.0'

  // required for xml parser (javax.xml.bind) (see: https://stackoverflow.com/questions/48204141/replacements-for-deprecated-jpms-modules-with-java-ee-apis/48279048#48279048)
  implementation 'javax.xml.bind:jaxb-api:2.4.0-b180830.0359' // xml parsing
  implementation 'org.apache.commons:commons-lang3:3.12.0' // for HashCodeBuilder
  implementation 'math.geom2d:javaGeom:0.11.1'
  implementation 'info.picocli:picocli:4.6.3' // command line interface

  spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.12.0'

  // testing
  testImplementation 'junit:junit:4.13.2'
  testImplementation 'cglib:cglib-nodep:3.3.0' // enables mocking of classes (in addition to interfaces)
  testImplementation 'org.mockito:mockito-core:4.8.0' // mocking framework
  testImplementation "org.scalatestplus:mockito-3-12_$scalaVersion:3.2.10.0" // syntactic sugar

  testImplementation 'org.spockframework:spock-core:2.2-groovy-4.0'
  testImplementation 'org.objenesis:objenesis:3.3' // Mock creation with constructor parameters

  // TODO: Remove. Only there for backwards compatibility in Java code
  implementation 'tech.units:indriya:2.1.3'
  implementation 'org.locationtech.jts:jts-core:1.19.0'
}

// scapegoat hook configuration
// https://github.com/sksamuel/scapegoat
// using compileScala instead of tasks.withType(ScalaCompile) prevents applying scapegoat to scala test classes
// see https://docs.gradle.org/current/userguide/scala_plugin.html#sec:configure_scala_classpath for details
tasks.withType(ScalaCompile) {
  scalaCompileOptions.additionalParameters = [
    "-Xplugin:" + configurations.scalaCompilerPlugin.asPath,
    "-P:scapegoat:dataDir:" + buildDir + "/reports/scapegoat/src/",
    "-P:scapegoat:disabledInspections:VariableShadowing",
    "-P:scapegoat:overrideLevels:LonelySealedTrait=warning",
    "-P:scapegoat:ignoredFiles:.*/SimonaConfig.scala" // see scapegoat-sbt page for this param
  ]
}

// separate scapegoat report for test classes
compileTestScala {
  scalaCompileOptions.additionalParameters = [
    "-Xplugin:" + configurations.scalaCompilerPlugin.asPath,
    "-P:scapegoat:dataDir:" + buildDir + "/reports/scapegoat/testsrc/",
    "-P:scapegoat:disabledInspections:VariableShadowing",
    "-P:scapegoat:overrideLevels:LonelySealedTrait=warning"
  ]
}

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

tasks.withType(Javadoc){
  options.encoding = 'UTF-8'
}

task printVersion {
  doLast {
    println project.version
  }
}

